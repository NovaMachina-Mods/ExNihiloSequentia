plugins {
    id "com.dorongold.task-tree" version "2.1.1"
}

apply from: "https://raw.githubusercontent.com/NovaMachina-Mods/conventions/1.20/scripts/init.gradle"
project.groupExcludes.addAll(
        'curse.maven',
        'mezz.jei',
        'mcjty.theoneprobe',
        'com.blamejared.crafttweaker',
        'dev.architectury',
        'dev.latvian.mods',
        'novamachina.*'
)
project.requiredMods.addAll(
        "novacore"
)
project.optionalMods.addAll(
        "jade",
        "jei",
        "the-one-probe"
)
project.replaceProperties.put("crafttweaker_version_range", "[14.0.12,)")
project.replaceProperties.put("jei_version_range", "[15.2.0.25,)")
project.replaceProperties.put("kubejs_version_range", "[0.0.0,)")

apply from: "https://raw.githubusercontent.com/NovaMachina-Mods/conventions/1.20/scripts/mod.gradle"

repositories {
//    mavenLocal()
    maven {
        name 'CraftTweaker'
        url "https://maven.blamejared.com"
        content {
            includeGroup 'com.blamejared.crafttweaker'
            includeGroup 'org.openzen.zencode'
            includeGroup 'mezz.jei'
        }
    }
    maven {
        name 'k-4u'
        url 'https://maven.k-4u.nl/'
        content {
            includeGroup 'mcjty.theoneprobe'
        }
    }
    maven {
        name 'CurseMaven'
        url 'https://www.cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
    maven {
        url 'https://maven.architectury.dev'
        content {
            includeGroup 'dev.architectury'
        }
    }
    maven {
        url 'https://maven.saps.dev/minecraft'
        content {
            includeGroup 'dev.latvian.mods'
        }
    }
    maven {
        url "https://jitpack.io"
        content {
            includeGroup "com.github.llamalad7.mixinextras"
        }
    }
}

dependencies {
    if (findProject(':NovaCore') != null) {
        implementation(project(':NovaCore')) {
            transitive = false
        }
    } else {
        implementation fg.deobf (project.dependencies.create("novamachina.novacore:novacore:${nova_core_version}") {
            transitive = false
        })
    }

    if(project.hasProperty("jei_version")) {
        compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}")
        implementation fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")
    }

    if(project.hasProperty("top_version")) {
        compileOnly fg.deobf("mcjty.theoneprobe:theoneprobe:${minecraft_version}-${top_version}:api") {
            transitive = false
        }
        runtimeOnly fg.deobf("mcjty.theoneprobe:theoneprobe:${minecraft_version}-${top_version}") {
            transitive = false
        }
    }

    if(project.hasProperty("jade_id")) {
        compileOnly fg.deobf("curse.maven:jade-api-324717:${jade_api_id}")
//        runtimeOnly fg.deobf("curse.maven:jade-324717:${jade_id}")
    }

    if(project.hasProperty("crafttweaker_version")) {
        implementation fg.deobf("com.blamejared.crafttweaker:CraftTweaker-forge-${minecraft_version}:${crafttweaker_version}")
        annotationProcessor 'com.blamejared.crafttweaker:Crafttweaker_Annotation_Processors:3.0.0.10'
        annotationProcessor "net.neoforged:forge:${minecraft_version}-${forge_version}"
        annotationProcessor "com.blamejared.crafttweaker:CraftTweaker-forge-${minecraft_version}:${crafttweaker_version}"
    }

    if(project.hasProperty("kubejs_version")) {
        implementation fg.deobf("dev.latvian.mods:kubejs-forge:${kubejs_version}")
        implementation fg.deobf("dev.latvian.mods:rhino-forge:${rhino_version}")
        implementation fg.deobf("dev.architectury:architectury-forge:${architectury_version}")
    }
//
}

processResources.dependsOn(compileJava)
compileJava {
    options.compilerArgs << "-Acrafttweaker.processor.document.output_directory=${file('docsOut')}"
    options.getGeneratedSourceOutputDirectory().set(file("src/main/resources/"))
}

jar.finalizedBy('reobfJar')
