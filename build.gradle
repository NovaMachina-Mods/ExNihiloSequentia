import se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'gradle.plugin.se.bjurr.gitchangelog', name: 'git-changelog-gradle-plugin', version: '1.66'
    }
}
plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}
apply plugin: 'net.minecraftforge.gradle'


def ENV = System.getenv()
String semver = "${version_major}.${version_api_major}.${version_api_minor}.${version_patch}"
version = "${minecraft_release}-" + new Date().format("yyyyMMddHHmmss")
group = "${artifact_group}.${artifact_basename}"
archivesBaseName = "${artifact_basename}"

sourceSets {
    main {
        resources {
            srcDir 'generated/resources'
        }
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

repositories {
    maven { url = 'https://files.minecraftforge.net/maven' }
    maven { url = "https://dvs1.progwml6.com/files/maven" } // JEI
    maven { url = "https://maven.tehnut.info"} // WAILA
    maven { url = "https://maven.tterrag.com/" } // TOP
    maven { url = "https://maven.blamejared.com" } // CraftTweaker
    maven {
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { url = "https://modmaven.dev"}
}


dependencies {
    minecraft([
            group: "net.minecraftforge",
            name: 'forge',
            version: "${minecraft_version}-${forge_major}.${forge_minor}.${forge_patch}"
    ])

    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}")

    compileOnly fg.deobf("mcp.mobius.waila:Hwyla:${hwyla_version}_${minecraft_release}.2:api")
    runtimeOnly fg.deobf("mcp.mobius.waila:Hwyla:${hwyla_version}_${minecraft_release}.2")

    compileOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-${minecraft_release}:${minecraft_release}-${top_version}:api")
    runtimeOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-${minecraft_release}:${minecraft_release}-${top_version}")

    compileOnly fg.deobf("com.blamejared.crafttweaker:CraftTweaker-${minecraft_version}:${crafttweaker_version}")
    runtimeOnly fg.deobf("com.blamejared.crafttweaker:CraftTweaker-${minecraft_version}:${crafttweaker_version}")

    runtimeOnly fg.deobf("curse.maven:ctm-267602:3137659")
}

task replaceResources(type: Copy) {
    outputs.upToDateWhen { false }
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand 'version': version, 'mc_version': minecraft_version_range, 'forge_version': forge_version_range
    }
    into "$buildDir/resources/main"
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
    finalizedBy replaceResources
}

minecraft {
    mappings channel: 'official', version: "${minecraft_version}"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                exnihilosequentia {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                exnihilosequentia {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'exnihilosequentia', '--all', '--output', file('generated/resources/'), '--existing', file('src/main/resources')

            mods {
                exnihilosequentia {
                    source sourceSets.main
                }
            }
        }
    }
}

task makeChangelog(type: GitChangelogTask) {
    file = file('changelog.html')
    untaggedName = "Current Release ${version}"
    fromCommit = "1be698f061a24f615c2a770d1190c995259ecbcd"
    toRef = 'HEAD'
    templateContent = file('changelog.mustache').getText('UTF-8')
    fromRepo = file(".")
}

curseforge {
    apiKey = ENV.CURSEFORGE_KEY ?: '0'

    project {
        id = "${curseforge_project}"
        releaseType = 'release'
        mainArtifact jar
        addGameVersion '1.16'
        addGameVersion '1.16.1'
        addGameVersion '1.16.2'
        addGameVersion '1.16.3'
        addGameVersion '1.16.4'
        addGameVersion '1.16.5'
        changelog = file('changelog.html')
        changelogType = 'html'
    }
}

afterEvaluate {
    tasks.curseforge400012.dependsOn makeChangelog
}

jar {
    manifest {
        attributes([
                "Specification-Title": "Ex Nihilo: Sequentia",
                "Specification-Vendor": "NovaMachina",
                "Specification-Version": version, // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": version,
                "Implementation-Vendor" :"NovaMachina",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "${artifact_group}"
            artifactId = "${artifact_basename}"
            version = "${version}"
            artifact jar
        }
    }
    repositories {
//        maven {
//            url "file:///${project.projectDir}/mcmodsrepo"
//        }
        maven {
            credentials {
                username = ENV.NEXUS_USERNAME
                password = ENV.NEXUS_PASSWORD
            }
            url = "https://repo.jacob-williams.me/repository/maven-releases/"
        }
    }
}